plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "java-library"
    id "com.modrinth.minotaur" version "2.+"
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true
        extraAccessWideners.add loom.accessWidenerPath.get().asFile.name

        mixinConfig "seamless-common.mixins.json"
        mixinConfig "seamless.mixins.json"
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"
    // Remove the next line if you don't want to depend on the API
    modApi "dev.architectury:architectury-forge:${rootProject.architectury_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

    modCompileOnlyApi("curse.maven:upgrade_aquatic_forge-326895:4415517") // Not updated
    modCompileOnlyApi("curse.maven:blueprint_forge-382216:4442628") // Not updated
    modCompileOnlyApi("curse.maven:supplementaries_forge-412082:4627388")
    modCompileOnlyApi("curse.maven:create_forge-328085:4626108")
    modCompileOnlyApi("curse.maven:biomes_o_plenty_forge-220318:4617259")
    modCompileOnlyApi("curse.maven:farmers_delight_forge-398521:3999157") // Not updated
    modCompileOnlyApi("curse.maven:haunted_harvest_forge-541753:4622565")
    modCompileOnlyApi("curse.maven:sleep_tight_forge-817564:4612817")
    modCompileOnlyApi("curse.maven:moonlight_forge-499980:4627905")
    modCompileOnlyApi("curse.maven:atmospheric_forge-362393:4415516") // Not updated
    modCompileOnlyApi("curse.maven:environmental_forge-388992:4415514") // Not updated
    modCompileOnlyApi("curse.maven:fast_paintings_forge-883287:4621175")
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "seamless"
    versionNumber = "${rootProject.mod_version}"
    versionName = "Seamless ${rootProject.mod_version} Forge"
    versionType = "release"
    uploadFile = remapJar
    gameVersions = ["1.20", "1.20.1"]
    loaders = ["forge"]
    dependencies {
        required.project "architectury-api"
    }
    changelog = rootProject.file("CHANGELOG_LATEST.md").text

    syncBodyFrom = rootProject.file("README.md").text
    tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
}

curseforge {
    project {
        apiKey = System.getenv('CURSEFORGE_TOKEN')
        id = '855593'
        changelog = rootProject.file("CHANGELOG_LATEST.md").text
        changelogType = "markdown"
        releaseType = 'release'
        addGameVersion '1.20'
        addGameVersion '1.20.1'

        relations {
            requiredDependency "architectury-api"
        }
        mainArtifact(remapJar) {
            displayName = "Seamless ${rootProject.mod_version} Forge"
        }
        options {
            debug = true
            javaVersionAutoDetect = false
            forgeGradleIntegration = false
        }
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    classifier "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier null
    archiveClassifier = "forge-${rootProject.version_names}"
}

jar {
    classifier "dev"
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

publishing {
    publications {
        mavenForge(MavenPublication) {
            artifactId = rootProject.archives_base_name + "-" + project.name
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
    }
}
